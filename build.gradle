version = '0.1'

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'application'
apply plugin: 'shadow'

mainClassName = 'com.martin.MainService'

// Use Java 7 by default
sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
	mavenCentral()
}

// so we can use the shadow plugin
buildscript {
	repositories {
		maven {
			name 'Shadow'
			url 'http://dl.bintray.com/content/johnrengelman/gradle-plugins'
		}
	}
	dependencies {
		classpath 'org.gradle.plugins:shadow:0.7.4'
	}
}

// Set our project variables
project.ext {
    app = 'groovyApp'
	springVersion = '3.2.4.RELEASE'
	dropwizardVersion = '0.6.2'
	dropWizardConfig = './groovyapp.yml'
}

dependencies {
	compile group: 'com.yammer.dropwizard', name: 'dropwizard-core', version: dropwizardVersion
	compile group: 'com.yammer.dropwizard', name: 'dropwizard-testing', version: dropwizardVersion
	compile group: 'com.yammer.dropwizard', name: 'dropwizard-client', version: dropwizardVersion
	compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.1.7'
	//compile group: 'com.sun.jersey', name: 'jersey-client', version: '1.17.1'
	//compile group: 'org.springframework', name: 'spring-context', version: springVersion

	testCompile group: 'junit', name: 'junit', version: '4.11'
	testCompile group: 'org.mockito', name: 'mockito-all', version: '1.9.5'
	testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
	testCompile group: 'org.easytesting', name: 'fest-assert-core', version: '2.0M10'
}

// heroku runs this to run your app
task stage(dependsOn: ['clean', 'shadow'])

// run the app out of the build dir
run {
	args 'server', dropWizardConfig
}

// run the jar file
task runShadow(type:Exec, dependsOn: 'shadow') {
    commandLine = ['java', '-jar', '-server', "./build/libs/$app-$version-shadow.jar",'server', dropWizardConfig]
}

shadow {
    baseName app
	exclude 'META-INF/*.SF'
	exclude 'META-INF/*.DSA'
	exclude 'META-INF/*.RSA'
}

jar {
	manifest {
		attributes 'Main-Class': mainClassName
	}
}

